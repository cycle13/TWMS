begin

lon1 = 39   ; WNP
lon2 = 181
lats = -20
latn = 30
tspan = 365 ; a year

;; read data
; Real time data 
fils = systemfunc ("ls /data5/hsushipei/data/total_field/SST/noaa_oisst_v2/sst.day.mean.*.v2.nc")
f = addfiles (fils( dimsizes(fils)-2:dimsizes(fils)-1 ) , "r")
ListSetType (f, "cat")
rel_t = f[:]->time

; convert relative to absolute time coordinate and remove leap day
abs_t = cd_calendar(rel_t, 2) ; with leap day
split_date = str_split_by_length( tostring(abs_t),(/4,2,2/) ) ; (year, month, day) , with leap day
no_leap_abst = abs_t(ind (.not.( (split_date(:,1).eq."02").and.(split_date(:,2).eq."29") ))) ; abs date without leap day
spl_nolp_abst = str_split_by_length( tostring(no_leap_abst),(/4,2,2/) ) ; (year, month, day) , without leap day 

;zeros = new(dimsizes(no_leap_abst), "integer")
;zeros = 0
;no_leap_relt = cd_inv_calendar(tointeger(spl_nolp_abst(:,0)), tointeger(spl_nolp_abst(:,1)), \
;                                tointeger(spl_nolp_abst(:,2)), zeros, zeros, zeros, rel_t@units, 0)

ntime = dimsizes(no_leap_abst) ; without leap day
sst = f[:]->sst(  ntime-tspan+1 : ntime ,{lats:latn},{lon1:lon2}) ; 365 x lat x lon, without leap day
sst&time = tointeger( no_leap_abst( ntime-tspan : ntime-1 ) ) ; 365 days
printVarSummary(sst)

sst_abst = sst&time
;abst_start = no_leap_abst( ntime-tspan ) ; absolute start date
;abst_end = no_leap_abst( ntime-1 ) ; absolute end date
;print("RT SST starts from "+abst_start+" to "+abst_end) 

; Climatology
f2 = addfile("/data5/hsushipei/data/total_field/SST/noaa_oisst_v2/oisst_daily_clim/oisst_1981_2017_daily_clim.nc","r")
sstc = f2->sst(:,{lats:latn},{lon1:lon2}) ; 365 x lat x lon, without leap day

;; extend period of climatology to match that of real time olr
spl_abst_start = str_split_by_length(sst_abst(0), (/4,4/)) ; year, mmdd
spl_abst_end = str_split_by_length(sst_abst(tspan-1), (/4,4/)) ; year, mmdd

sstc1 = sstc({"2016"+spl_abst_start(1):20161231},:,:)
sstc2 = sstc({20160101:"2016"+spl_abst_end(1)},:,:)
sstc_ext = array_append_record(sstc1, sstc2,0) ; (365, lat, lon)
printVarSummary(sstc_ext)
delete(sstc)
delete(sstc1)
delete(sstc2)

;;---- calculate anomaly
ssta = sst ; copy coordinates, time x lat x lon
ssta = sst - sstc_ext
ssta@long_name = "SST anomaly"
printVarSummary(ssta)

;;---- calculate each penta
p1p_d = sst_abst( tspan-5:tspan-1 )
p2p_d = sst_abst( tspan-10:tspan-6 )
p3p_d = sst_abst( tspan-15:tspan-11 )
p4p_d = sst_abst( tspan-20:tspan-16 )
p5p_d = sst_abst( tspan-25:tspan-21 )
p6p_d = sst_abst( tspan-30:tspan-26 )

p1pm =  dim_avg_n_Wrap( ssta( {p1p_d},:,: ), 0 )
p2pm =  dim_avg_n_Wrap( ssta( {p2p_d},:,: ), 0 )
p3pm =  dim_avg_n_Wrap( ssta( {p3p_d},:,: ), 0 )
p4pm =  dim_avg_n_Wrap( ssta( {p4p_d},:,: ), 0 )
p5pm =  dim_avg_n_Wrap( ssta( {p5p_d},:,: ), 0 )
p6pm =  dim_avg_n_Wrap( ssta( {p6p_d},:,: ), 0 )
printVarSummary(p1pm)

;;----resource
res=True
res@gsnDraw      = False                ; don't draw yet
res@gsnFrame     = False                ; don't advance frame yet
res@mpMinLatF    = lats       ; Set the domain of map (not the domain
res@mpMaxLatF    = latn       ; of data). If the data's domain is
res@mpMinLonF    = lon1       ; smaller than the map's, you'll see
res@mpMaxLonF    = lon2       ; blank(white) area on plot (missing).
res@mpCenterLonF = (lon1+lon2)/2 ; Center lon of this plot.
res@gsnAddCyclic = False ; if the map domain isn't global
res@tmXBTickSpacingF = 20
res@tmYLTickSpacingF = 10
res@tmXBLabelFont = 0
res@tmXBLabelFontHeightF = 0.013
res@tmYLLabelFont = 0
res@tmYLLabelFontHeightF = 0.013
res@cnFillOn  = True ; turn on contour fill
res@cnLinesOn = False ; turn off contour line
res@cnLevelSelectionMode = "ExplicitLevels"
res@cnLevels = (/-5, -4, -3, -2, -1, -0.5, 0, 0.5, 1 ,2,3, 4, 5/)
res@lbLabelFont = 0 ; font for label bar
;res@pmLabelBarOrthogonalPosF = 0.4 ;  color bar y position
;res@gsnMaximize = True
res@lbLabelBarOn  = False  ; turn off individual color bar
res@cnLineLabelFontHeightF = 1. ; seems to remove contour labels as individual color bars are removed
;res@gsnStringFont = 0 ; font for gsnright/center/leftstring
res@gsnRightStringFontHeightF = 0.015

; resource for panel 
resP = True
resP@gsnMaximize = True ; maximize plot
resP@gsnPanelLabelBar = True ; adds a common label bar at the bottom of the plot
resP@lbLabelFont = 0 ; font type of labels of common color bar  
resP@lbLabelFontHeightF = 0.01 ; font height of labels of common color bar  
resP@pmLabelBarOrthogonalPosF = 0.0055 ; y position of label bar
resP@txString = "OISST anomaly for the Past" ; add common title
resP@txFontHeightF = 0.012 ; font size for common title
resP@txFont = 13 ; font for common title

txres = True
txres@txFontHeightF = 0.02 ; font height

mkres = True
mkres@gsMarkerIndex = 16
mkres@gsMarkerThicknessF = 1.5

;;----plot
pltdir = "/data5/hsushipei/tropical_weather_monitoring_system/daily_weather_map/sst_anoamly_hist/"
pltn = "oisst_anomaly_hist"
wks = gsn_open_wks("png", pltdir+pltn)
gsn_define_colormap(wks,"NCV_blue_red")   ; define color map
plot = new(6,graphic) ; create plot array
lines = new(6,graphic)
 
delete(p1pm@long_name)
delete(p1pm@standard_name)
delete(p1pm@units)
p1pm@long_name = "5d avg(0)"
res@gsnRightString = p1p_d(0)+"-"+p1p_d( dimsizes(p1p_d)-1 ) ; showing period
plot1 = gsn_csm_contour_map_ce(wks, p1pm, res)
lines(0) = gsn_add_polyline(wks, plot1, (/lon1, lon2/), (/0,0/), False)
plot(0) = plot1

delete(p2pm@long_name)
delete(p2pm@standard_name)
delete(p2pm@units)
p2pm@long_name = "5d avg(-1)"
res@gsnRightString = p2p_d(0)+"-"+p2p_d( dimsizes(p2p_d)-1 ) ; showing period
plot1 = gsn_csm_contour_map_ce(wks, p2pm, res)
lines(1) = gsn_add_polyline(wks, plot1, (/lon1, lon2/), (/0,0/), False)
plot(1) = plot1

delete(p3pm@long_name)
delete(p3pm@standard_name)
delete(p3pm@units)
p3pm@long_name = "5d avg(-2)"
res@gsnRightString = p3p_d(0)+"-"+p3p_d( dimsizes(p3p_d)-1 ) ; showing period
plot1 = gsn_csm_contour_map_ce(wks, p3pm, res)
lines(2) = gsn_add_polyline(wks, plot1, (/lon1, lon2/), (/0,0/), False)
plot(2) = plot1

delete(p4pm@long_name)
delete(p4pm@standard_name)
delete(p4pm@units)
p4pm@long_name = "Last 4 pentad"
res@gsnRightString = p4p_d(0)+"-"+p4p_d( dimsizes(p4p_d)-1 ) ; showing period
plot1 = gsn_csm_contour_map_ce(wks, p4pm, res)
lines(3) = gsn_add_polyline(wks, plot1, (/lon1, lon2/), (/0,0/), False)
plot(3) = plot1

delete(p5pm@long_name)
delete(p5pm@standard_name)
delete(p5pm@units)
p5pm@long_name = "Last 5 pentad"
res@gsnRightString = p5p_d(0)+"-"+p5p_d( dimsizes(p5p_d)-1 ) ; showing period
plot1 = gsn_csm_contour_map_ce(wks, p5pm, res)
lines(4) = gsn_add_polyline(wks, plot1, (/lon1, lon2/), (/0,0/), False)
plot(4) = plot1

delete(p6pm@long_name)
delete(p6pm@standard_name)
delete(p6pm@units)
p6pm@long_name = "Last 6 pentad"
res@gsnRightString = p6p_d(0)+"-"+p6p_d( dimsizes(p6p_d)-1 ) ; showing period
plot1 = gsn_csm_contour_map_ce(wks, p6pm, res)
lines(5) = gsn_add_polyline(wks, plot1, (/lon1, lon2/), (/0,0/), False)
plot(5) = plot1

;gsn_panel(wks,(/plot(0),plot(1),plot(2),plot(3),plot(4),plot(5)/),(/3,2/),resP)
gsn_panel(wks,(/plot(2),plot(1),plot(0)/),(/3,1/),resP)

print("Plot created-> "+pltn)
print("%%%% Leave 'plot_sst_anom_hist.ncl' %%%% ")

end
